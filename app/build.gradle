plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.bme.aut.craftedcocktails"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        OrmTest {
            applicationId "com.bme.aut.craftedcocktails.ormTest"
            dimension "default"
            versionName "1.0-test"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        test.java.srcDirs += 'src/test/java'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    def android_core_ktx_version = '1.3.2'
    def androidx_appcompat_version = '1.2.0'
    def android_material_version = '1.3.0'
    def androidx_constraintlayout_version = '2.0.4'
    def androidx_legacy_version = '1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$android_core_ktx_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "com.google.android.material:material:$android_material_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_version"
    implementation "androidx.legacy:legacy-support-v4:$androidx_legacy_version"

    /**
     * RainbowCake
     * https://github.com/rainbowcake/rainbowcake
     */
    def rainbow_cake_version = '1.3.0'
    implementation "co.zsmb:rainbow-cake-core:$rainbow_cake_version" // Core library (required)
    implementation "co.zsmb:rainbow-cake-dagger:$rainbow_cake_version" // Dagger 2 support
    implementation "co.zsmb:rainbow-cake-koin:$rainbow_cake_version" // Koin support
    implementation "co.zsmb:rainbow-cake-navigation:$rainbow_cake_version" // Navigation features
    implementation "co.zsmb:rainbow-cake-timber:$rainbow_cake_version"
    // Internal logging through Timber
    testImplementation "co.zsmb:rainbow-cake-test:$rainbow_cake_version" // Testing utilities

    /**
     * Coroutines
     * https://github.com/Kotlin/kotlinx.coroutines
     */
    def coroutines_version = '1.4.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    /**
     * Dagger
     * https://github.com/google/dagger
     */
    def dagger_version = '2.29.1'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    /**
     * Network
     * https://github.com/swagger-api/swagger-core
     * https://github.com/square/okhttp
     * https://github.com/google/gson
     * https://github.com/square/retrofit
     */
    def swagger_version = '1.6.2'
    def okhttp_version = '4.9.1'
    def gson_version = '2.8.6'
    def retrofit_version = '2.9.0'
    implementation "io.swagger:swagger-annotations:$swagger_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    /**
     * Testing Navigation
     * Feature module Support
     * https://developer.android.com/guide/navigation
     */
    def nav_version = '2.3.5'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    /**
     * Room
     * https://developer.android.com/training/data-storage/room
     */
    def room_version = '2.3.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    /**
     * Android lifecycle
     * https://developer.android.com/jetpack/androidx/releases/lifecycle
     */
    def androidx_lifecycle_extension_version = '2.2.0'
    def androidx_lifecycle_runtime_version = '2.3.1'
    implementation "androidx.lifecycle:lifecycle-extensions:$androidx_lifecycle_extension_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_runtime_version"

    /**
     * Material Drawer
     * https://github.com/zsmb13/MaterialDrawerKt
     */
    def material_drawer_version = '3.0.0'
    def recyclerview_version = '1.2.0'
    def androidx_annotation_version = '1.2.0'
    def material_typeface_version = '3.0.1.4.original-kotlin@aar'
    implementation "co.zsmb:materialdrawer-kt:$material_drawer_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.annotation:annotation:$androidx_annotation_version"
    implementation "com.mikepenz:google-material-typeface:$material_typeface_version"

    /**
     * Card View
     * https://developer.android.com/guide/topics/ui/layout/cardview
     */
    def card_view_version = '1.0.0'
    implementation "androidx.cardview:cardview:$card_view_version"

    /**
     * Glide
     * https://github.com/bumptech/glide
     */
    def glide_version = '4.12.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"

    /**
     * Awesome Dialog
     * https://github.com/chnouman/AwesomeDialog
     */
    def awesome_dialog_version = '1.0.5'
    implementation "com.github.chnouman:AwesomeDialog:$awesome_dialog_version"

    /**
     * Swipe Refresh Layout
     * https://developer.android.com/jetpack/androidx/releases/swiperefreshlayout
     */
    def swipe_refresh_layout_version = '1.2.0-alpha01'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_layout_version"

    /**
     * Test
     */
    def test_support_version = '1.0.2'
    def junit_version = '4.13.2'
    def mockito_version = '3.6.28'
    def espresso_version = '3.0.2'
    def robolectric_version = '4.4'
    def support_version = '28.0.0'
    def androidx_test_version = '1.1.2'
    def androidx_espresso_version = '3.3.0'
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "com.android.support.test:runner:$test_support_version"
    androidTestImplementation "com.android.support.test:rules:$test_support_version"
    androidTestImplementation "com.android.support:support-annotations:$support_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation 'javax.annotation:jsr250-api:1.0'
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_version"

}